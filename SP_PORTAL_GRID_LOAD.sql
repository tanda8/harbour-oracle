create or replace PROCEDURE SP_PORTAL_GRID_LOAD
(
	i_USER_ID IN VARCHAR2,
	i_SEARCH_TYPE IN VARCHAR2,
	i_SEARCH_TERM  IN VARCHAR2,
	o_CURSOR OUT SYS_REFCURSOR
)
AS

	v_USER_ID CONSTANT NUMBER := TO_NUMBER(i_USER_ID);   -- cast required due to .NET EF bug
	v_COMPANY_ID NUMBER;
	v_COMPANY_TYPE NUMBER;

BEGIN

	SELECT COMPANY.CO_ID, COMPANY.CO_TYPE
	INTO v_COMPANY_ID, v_COMPANY_TYPE
	FROM COMPANY, CONTACTS
	WHERE CONTACTS.CONT_CO_ID = COMPANY.CO_ID
		AND CONTACTS.CONT_ID = v_USER_ID;

	IF (v_COMPANY_TYPE != 1 AND v_COMPANY_TYPE != 2) THEN  -- not a Shipper or Consignee company
		RETURN;
	END IF;

	OPEN o_CURSOR FOR
	SELECT DISTINCT
		RQBK_IDX.RQI_RQBK_NUM HARBOUR_NUMBER,
		TRUNC(RQBK_IDX.RQI_DATE_REQUESTED) REQUEST_DATE,
		INITCAP(DECODE(v_COMPANY_TYPE, 1, DECODE(RQBK_SH_CS.RQSC_CS_ID, NULL, RQBK_SH_CS.RQSC_CS_NAME, COMPANY_CONSIGNEE.CO_NAME), 2, DECODE(RQBK_SH_CS.RQSC_SH_ID, NULL, RQBK_SH_CS.RQSC_SH_NAME, COMPANY_SHIPPER.CO_NAME))) COMPANY_NAME,
		DECODE(RQBK_SH_CS.RQSC_POD_NAME, NULL, DECODE(RQBK_SH_CS.RQSC_POD, NULL, AIRPORTS_POD.AP_CODE, CITY_POD.CT_DESC || DECODE(STATES_POD.STATE_CODE, NULL, ', ', ', ' || STATES_POD.STATE_CODE || ', ') || COUNTRY_POD.CY_CODE), RQBK_SH_CS.RQSC_POD_NAME) PLACE_OF_DELIVERY,
		DECODE(RQBK_SH_CS.RQSC_POR_NAME, NULL, DECODE(RQBK_SH_CS.RQSC_POR, NULL, AIRPORTS_POR.AP_CODE, CITY_POR.CT_DESC || DECODE(STATES_POR.STATE_CODE, NULL, ', ', ', ' || STATES_POR.STATE_CODE || ', ') || COUNTRY_POR.CY_CODE), RQBK_SH_CS.RQSC_POR_NAME) PLACE_OF_RECEIPT,
		DECODE(RQBK_IDX.RQI_FCL_LCL, NULL, 'Air', 1, FUNCTION_PORTAL_FCL_TYPES(RQBK_IDX.RQI_RQBK_ID, 2), RQBK_CONTAINER_TYPE.RQCT_DESC) FILL_TYPE,
		INITCAP(RQBK_MC.RQM_COMM_NAME) COMMODITY,
		HAZARD_PERISH_TYPES.HPT_HAZARDOUS HAZARD_FLAG
	FROM
		RQBK_IDX,
		RQBK_SH_CS,
		RQBK_CONTAINER_TYPE,
		RQBK_MC,
		HAZARD_PERISH_TYPES,
		COMPANY COMPANY_SHIPPER,
		COMPANY COMPANY_CONSIGNEE,
		CITY CITY_POD,
		CITY CITY_POR,
		STATES STATES_POD,
		STATES STATES_POR,
		COUNTRY COUNTRY_POD,
		COUNTRY COUNTRY_POR,
		AIRPORTS AIRPORTS_POD,
		AIRPORTS AIRPORTS_POR,

		RQBK_OC_CARRIER,
		RQBK_REF,
		RQBK_AIR_CARRIER

	WHERE RQBK_IDX.RQI_COMPLETE = -1   -- Completed Transaction
		AND RQBK_IDX.RQI_ACTIVE = -1     -- Active Transaction
		AND RQBK_IDX.RQI_RQBK = 2        -- Booking Type (RateQuote=1, Booking=2)
		AND RQBK_IDX.RQI_RQBK_ID = RQBK_SH_CS.RQSC_RQBK_ID (+)
		AND RQBK_IDX.RQI_RQBK_ID = RQBK_MC.RQM_RQBK_ID (+)
		AND RQBK_IDX.RQI_FCL_LCL = RQBK_CONTAINER_TYPE.RQCT_ID (+)
		AND RQBK_MC.RQM_HAZ_PERISH = HAZARD_PERISH_TYPES.HPT_ID (+)

		AND RQBK_SH_CS.RQSC_POD = CITY_POD.CT_ID (+)
		AND CITY_POD.CT_STATE_ID = STATES_POD.STATE_ID (+) 
		AND CITY_POD.CT_COUNTRY_ID = COUNTRY_POD.CY_ID (+)
		AND RQBK_SH_CS.RQSC_POD_AIR = AIRPORTS_POD.AP_ID (+)

		AND RQBK_SH_CS.RQSC_POR = CITY_POR.CT_ID (+)
		AND CITY_POR.CT_STATE_ID = STATES_POR.STATE_ID (+)
		AND CITY_POR.CT_COUNTRY_ID = COUNTRY_POR.CY_ID (+)
		AND RQBK_SH_CS.RQSC_POR_AIR = AIRPORTS_POR.AP_ID (+)

		AND RQBK_SH_CS.RQSC_SH_ID = COMPANY_SHIPPER.CO_ID (+) 
		AND RQBK_SH_CS.RQSC_CS_ID = COMPANY_CONSIGNEE.CO_ID (+)
		AND DECODE(v_COMPANY_TYPE, 1, RQBK_SH_CS.RQSC_SH_ID, 2, RQBK_SH_CS.RQSC_CS_ID) = v_COMPANY_ID   -- Decode: Shipper = 1, Consignee = 2

		AND RQBK_IDX.RQI_RQBK_ID = RQBK_OC_CARRIER.RQOC_RQBK_ID (+)
		AND RQBK_IDX.RQI_RQBK_ID = RQBK_REF.RQR_RQBK_ID (+)
		AND RQBK_IDX.RQI_RQBK_ID = RQBK_AIR_CARRIER.RQAC_RQBK_ID (+)

		AND (
				(i_SEARCH_TYPE = 'CI')   -- Company ID (return everything)
			OR (i_SEARCH_TYPE = 'HN' AND RQI_RQBK_NUM = i_SEARCH_TERM)   -- Harbour Number
			OR (i_SEARCH_TYPE = 'CN' AND DECODE(v_COMPANY_TYPE, 2, RQBK_SH_CS.RQSC_SH_ID, 1, RQBK_SH_CS.RQSC_CS_ID) = i_SEARCH_TERM)   -- Company Name
			OR (i_SEARCH_TYPE = 'POD' AND (RQBK_SH_CS.RQSC_POD_AIR = i_SEARCH_TERM OR RQBK_SH_CS.RQSC_POD = i_SEARCH_TERM))   -- Place of Delivery (ocean and air)      
			OR (i_SEARCH_TYPE = 'CBN' AND RQBK_OC_CARRIER.RQOC_BK_NUM = i_SEARCH_TERM)   -- Carrier Booking Number      
			OR (i_SEARCH_TYPE = 'SRN' AND RQBK_REF.RQR_REF_TYPE = 1 AND RQBK_REF.RQR_REF_NUM = i_SEARCH_TERM)   -- Shipper Reference Number
			OR (i_SEARCH_TYPE = 'CRN' AND RQBK_REF.RQR_REF_TYPE = 2 AND RQBK_REF.RQR_REF_NUM = i_SEARCH_TERM)   -- Consignee Reference Number
			OR (i_SEARCH_TYPE = 'AWN' AND RQBK_AIR_CARRIER.RQAC_AWB_NUM = i_SEARCH_TERM)   -- Air Waybill Number      
		)

	ORDER BY REQUEST_DATE DESC;

	EXCEPTION WHEN OTHERS THEN
		RAISE;
END;